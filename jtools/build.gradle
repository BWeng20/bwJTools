plugins {
    id 'java'
    id 'maven-publish'
    id 'signing'
}

apply plugin: 'java'
apply plugin: 'maven-publish'

repositories {
    mavenCentral()
}

sourceSets {
    main{
        java.srcDir file('src/main/java')
        resources.srcDir file('src/main/resources')
    }
    test {
        java.srcDir file('src/test/java')
        compileClasspath += sourceSets.main.output
        runtimeClasspath += sourceSets.main.output
    }
}

dependencies {
    implementation 'org.apache.logging.log4j:log4j-api:2.12.0'
    implementation 'org.apache.logging.log4j:log4j-core:2.12.0'
    implementation 'javax.json:javax.json-api:1.1.4'
    implementation 'org.apache.commons:commons-csv:1.8'
    implementation 'javax.interceptor:javax.interceptor-api:1.2'
    implementation 'org.glassfish:javax.json:1.1.4'
    implementation 'org.netbeans.api:org-netbeans-swing-outline:RELEASE121'
    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.3.1'
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.3.1'
}

task sourcesJar(type: Jar, dependsOn: classes) {
    archiveClassifier = 'sources'
    from sourceSets.main.allSource
}

compileJava {
    doFirst {
        println "Gradle version $project.gradle.gradleVersion"

        options.compilerArgs = [
                '--module-path', classpath.asPath,
                '--add-exports', 'java.desktop/sun.awt.shell=com.bw.jtools'
        ]
        classpath = files()
    }
}

task intTest(type: Test) {
    description = 'Runs tests.'
    group = 'verification'
    testClassesDirs = sourceSets.test.output.classesDirs
    classpath = sourceSets.test.runtimeClasspath
    shouldRunAfter test
    useJUnitPlatform()
}

check.dependsOn intTest

processResources {
    duplicatesStrategy = 'exclude'
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    archiveClassifier = 'javadoc'
    from javadoc.destinationDir
}

jar {
    manifest {
        attributes 'Implementation-Title': 'JTools',  
        	   'Implementation-Version': project.version,
                   'Build-Timestamp': new java.text.SimpleDateFormat("yyyy-MM-dd'T'HH:mm:ss.SSSZ").format(new Date()),
                   'Build-Jdk'      : "${System.properties['java.version']} (${System.properties['java.vendor']} ${System.properties['java.vm.version']})"
    }

    archiveBaseName = 'jtools';
    includeEmptyDirs = false;
    from "${rootProject.projectDir}/licenses"

}

publishing {
    repositories {        
        maven {
            url publishUrl
            credentials {
                // Place this two variable with the correct values in your 
                // <Gradle-User-Home>/gradle.properties
                username sonatypeUsername
                password sonatypePassword
            }
        }
    }
    publications {
        jtools(MavenPublication) {
            artifactId = 'jtools'
            from components.java
            
            artifacts = [ jar, sourcesJar, javadocJar ]
            
            pom {
                name = 'jtools'
                description = 'A minimalistic set of tools for rapid application development'
                url = 'https://github.com/BWeng20/bwJTools'
                licenses {
                    license {
                        name = 'MIT License'
                    }
                }
                developers {
                    developer {
                        id = 'bweng20'
                        name = 'Bernd Wengenrot'
                        email = 'Bernd.Wengenrot@gmail.com'
                    }
                }
                scm {
                    connection = 'scm:git:git://github.com/BWeng20/bwJTools.git'
                    developerConnection = 'scm:git:ssh://github.com:BWeng20/bwJTools.git'
                    url = 'https://github.com/BWeng20/bwJTools'
                }                
            }
        }
    }
}
    
signing {
    sign publishing.publications.jtools
}

task runJPathChooser(type: Exec) {
    dependsOn build
    dependsOn ":jtools:jar"
    group = "Examples"
    description = "Runs JPathChooser"

    commandLine "java",
            "-agentlib:jdwp=transport=dt_socket,server=y,suspend=n,address=127.0.0.1:8000",
            "-classpath", "${buildPath}/../../jtools/build/libs/jtools-${project.version}.jar",
            "com.bw.jtools.ui.pathchooser.JPathChooser"
}