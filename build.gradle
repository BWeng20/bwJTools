plugins {
    id 'maven-publish'
    id 'signing'
}

tasks.withType(JavaCompile) {
    sourceCompatibility= JavaVersion.VERSION_1_8
    targetCompatibility= JavaVersion.VERSION_1_8
}

apply plugin: 'java'
apply plugin: 'maven'

repositories {
    mavenCentral()
}

sourceSets {
    // Define different source sets, one for each artefact that we
    // need to publish.
    jProfilingAgent{
        java {
            srcDir 'src/main/java' 
            include '**/com/bw/jtools/Log**', 
                    '**/com/bw/jtools/profiling/measurement/**', 
                    '**/com/bw/jtools/profiling/MethodProfiling.java',
                    '**/com/bw/jtools/profiling/**Information.java',
                    '**/com/bw/jtools/profiling/ReflectionProfilingUtil.java',
                    '**/com/bw/jtools/profiling/weaving/**',
                    '**/com/bw/jtools/log/**', '**/net/bytebuddy/**'
            exclude '**/com/bw/jtools/profiling/callgraph/**'
        }
        resources {
            srcDir 'src/main/resources'
        }
    }
    jToolsExamples{
        java {
            srcDir 'src/main/java' 
            include '**/com/bw/jtools/**'
            exclude '**/com/bw/jtools/profiling/weaving/**'
        }
        resources {
            srcDir 'src/main/resources'
        }
    }
    jTools {
        java {
            srcDir 'src/main/java' 
            exclude '**/com/bw/jtools/profiling/weaving/**'
            exclude '**/com/bw/jtools/examples/**'
        }
        resources {
            srcDir 'src/main/resources'
        }
    }
}

dependencies {
    
    compile group:'org.apache.logging.log4j', name:'log4j-api', version:'2.12.0'
    compile group:'org.apache.logging.log4j', name:'log4j-core', version:'2.12.0'
    compile group:'javax.json', name:'javax.json-api', version:'1.1.4'
    compile group:'org.apache.commons', name:'commons-csv', version:'1.1'
    compile group:'javax.interceptor', name:'javax.interceptor-api', version:'1.2'
    compile group:'org.glassfish', name:'javax.json', version:'1.1.4'
    compile group:'org.netbeans.api', name:'org-netbeans-swing-outline', version:'RELEASE110'
/*
    jToolsCompile( group:'org.netbeans.api', name:'org-netbeans-swing-outline', version:'RELEASE110' )
    jToolsCompile( group:'org.apache.logging.log4j', name:'log4j-api', version:'2.12.0' )
    jToolsCompile( group:'org.apache.logging.log4j', name:'log4j-core', version:'2.12.0')
    jToolsCompile( group:'javax.json', name:'javax.json-api', version:'1.1.4')
    jToolsCompile( group:'org.apache.commons', name:'commons-csv', version:'1.1')
    jToolsCompile( group:'javax.interceptor', name:'javax.interceptor-api', version:'1.2')
    jToolsCompile( group:'org.glassfish', name:'javax.json', version:'1.1.4')
*/    
    // jToolsCompile 'org.netbeans.api:org-netbeans-swing-outline:RELEASE110:sources'
    // jToolsCompile 'net.bytebuddy:byte-buddy:1.10.6:sources'
       
    jProfilingAgentCompile( group:'org.apache.logging.log4j', name:'log4j-api', version:'2.12.0')
    jProfilingAgentCompile( group:'org.apache.logging.log4j', name:'log4j-core', version:'2.12.0')
    jProfilingAgentCompile( group:'net.bytebuddy', name:'byte-buddy', version:'1.10.6')
    // jProfilingAgentCompile 'net.bytebuddy', name:'byte-buddy:1.10.6:javadoc'

    compile( group:'net.bytebuddy', name:'byte-buddy', version:'1.10.6')

}

configurations {   
    jToolsExamplesCompile.extendsFrom compile
    jToolsCompile.extendsFrom compile
}


group = "io.github.bweng20"
project.version="1.01"

task uiProfilingJar(type: Jar) {
    description = "Create a jar for Profiling Scanner UI."
    group = "Build"
    manifest {
        attributes 'Implementation-Title': 'BJProfiling Scanner UI',  
        	   'Implementation-Version': project.version,
                   'Build-Timestamp': new java.text.SimpleDateFormat("yyyy-MM-dd'T'HH:mm:ss.SSSZ").format(new Date()),
                   'Build-Jdk'      : "${System.properties['java.version']} (${System.properties['java.vendor']} ${System.properties['java.vm.version']})",
        	   'Main-Class': "com.bw.jtools.ui.profiling.ProfilingLogScannerUI"
    }
    includes = [ 'NOTICE','licenses/**',
                 '**/com/bw/jtools/*',
                 '**/com/bw/jtools/io/**',
                 '**/com/bw/jtools/persistence/**',
                 '**/com/bw/jtools/log/**',
                 '**/com/bw/jtools/profiling/**',
                 '**/com/bw/jtools/ui/profiling/**',
                 '**/com/bw/jtools/ui/*',
                 '**/javax/json/**',
                 '**/org/glassfish/**'
               ];

    excludes = [ '**/com/bw/jtools/profiling/weaving/**' ];
    baseName = 'bwJProfilingUI';
    includeEmptyDirs = false;
    from 'NOTICE'
    from 'licenses/**'
    from { configurations.jTools.collect { it.isDirectory() ? it : zipTree(it) } }
    with jar
}

task jToolsExamplesJar(type: Jar) {
    dependsOn jToolsExamplesClasses
    description = "Create a jar with examples."
    group = "Build"
    baseName = 'jToolsExamples';
    includeEmptyDirs = false
    
    excludes = [ '**/com/bw/jtools/profiling/weaving/**',
                 '**/org/apache/logging/**',
                 '**/net/bytebuddy/**' ];   
    
    includes = [ 'NOTICE','licenses/**',
                 '**/com/bw/jtools/**', 
                 '**/javax/interceptor/**' , '**/javax/json/**',
                 '**/org/apache/commons/**', 
                 '**/org/glassfish/**', '**/org/netbeans/**'
               ]
    manifest {
        attributes 'Implementation-Title': 'bwJTools Examples',  
        	   'Implementation-Version': project.version,
                   'Build-Timestamp': new java.text.SimpleDateFormat("yyyy-MM-dd'T'HH:mm:ss.SSSZ").format(new Date()),
                   'Build-Jdk'      : "${System.properties['java.version']} (${System.properties['java.vendor']} ${System.properties['java.vm.version']})",
        	   'Main-Class': "com.bw.jtools.examples.starter.ExampleList"
    }    

    from 'NOTICE'
    from 'licenses/**'
//    from sourceSets.jToolsExamples.output
    from { sourceSets.jToolsExamples.runtimeClasspath.collect { it.isDirectory() ? it : zipTree(it) } }
}

task jToolsJar(type: Jar) {
    description = "Create a jar for jTools."
    group = "jTools"
    manifest {
        attributes 'Implementation-Title': 'JTools',  
        	   'Implementation-Version': project.version,
                   'Build-Timestamp': new java.text.SimpleDateFormat("yyyy-MM-dd'T'HH:mm:ss.SSSZ").format(new Date()),
                   'Build-Jdk'      : "${System.properties['java.version']} (${System.properties['java.vendor']} ${System.properties['java.vm.version']})"
    }
    includes = [ 'NOTICE','licenses/**', '**/com/bw/jtools/**' ];
    excludes = [ '**/com/bw/jtools/examples/**' ];

    baseName = 'jTools';
    includeEmptyDirs = false;
    from 'NOTICE'
    from 'licenses/**'
    from sourceSets.jTools.output
}

task jToolsJavadoc(type: Javadoc) {
    source = sourceSets.jTools.allJava
    group = "jTools"
    classpath = sourceSets.jTools.runtimeClasspath 
    failOnError = false
    doLast {
        copy {
            from "src/main/java"
            include "**/doc-files/**"
            into "$buildDir/docs/javadoc"
        }
    }
}

task jToolsJavadocJar(type: Jar) {
    from jToolsJavadoc
    group = "jTools"
    baseName = 'jTools'
    classifier = 'javadoc'
    includeEmptyDirs = false;
}

task jToolsSourcesJar(type: Jar) {
    from sourceSets.jTools.allJava
    group = "jTools"
    baseName = 'jTools'
    classifier = 'sources'
    includeEmptyDirs = false;
}

task jProfilingAgentJar(type: Jar) {
    dependsOn jProfilingAgentClasses
    description = "Create a jar only with Profiling stuff - including weaving."
    group = "jProfilingAgent"
    baseName = "jProfilingAgent"
    includes = [ 'NOTICE', '**/com/bw/jtools/Log**', '**/com/bw/jtools/profiling/**', '**/com/bw/jtools/log/**', '**/net/bytebuddy/**' ]
    includeEmptyDirs = false
    manifest {
        attributes 'Implementation-Title': 'jtools',  
        	   'Implementation-Version': project.version,
                   'Build-Timestamp': new java.text.SimpleDateFormat("yyyy-MM-dd'T'HH:mm:ss.SSSZ").format(new Date()),
                   'Build-Jdk'      : "${System.properties['java.version']} (${System.properties['java.vendor']} ${System.properties['java.vm.version']})",
                   'Premain-Class'  : "com.bw.jtools.profiling.weaving.ProfilingWeaver"
    }    
    
    from 'NOTICE'
    from 'licenses/**'
    from { sourceSets.jProfilingAgent.runtimeClasspath.collect { it.isDirectory() ? it : zipTree(it) } }

}

task jProfilingAgentJavadoc(type: Javadoc) {
    source = sourceSets.jProfilingAgent.allJava
    group = "jProfilingAgent"
    classpath = sourceSets.jProfilingAgent.runtimeClasspath 
    failOnError = false
    doLast {
        copy {
            from "src/main/java"
            include "**/doc-files/**"
            into "$buildDir/docs/javadoc"
        }
    }  
}

task jProfilingAgentJavadocJar(type: Jar) {
    from jProfilingAgentJavadoc
    group = "jProfilingAgent"
    baseName = "jProfilingAgent"
    classifier = 'javadoc'
    includeEmptyDirs = false
}

task jProfilingAgentSourcesJar(type: Jar) {
    from sourceSets.jProfilingAgent.allJava
    group = "jProfilingAgent"
    classifier = 'sources'
    baseName = "jProfilingAgent"
    includeEmptyDirs = false
}

configurations {
    runtimeOnly {
        exclude group: 'org.apache.logging.log4j', module: 'log4j-api'
        exclude group: 'org.apache.logging.log4j', module: 'log4j-core'
        /* exclude group: 'org.apache.commons', module: 'commons-csv' --> */
    }
}



task runAppIconDemo(type: Exec) {
    dependsOn jToolsExamplesJar
    group = "Examples"
    description = "Runs Aplication Icons Demo"
    commandLine "java", "-classpath", sourceSets.jToolsExamples.runtimeClasspath.getAsPath(), "com.bw.jtools.examples.applicationicons.ApplicationIconsDemo" 
}

task runDataDemo(type: Exec) {
    dependsOn jToolsExamplesJar
    group = "Examples"
    description = "Runs DataDemo"
    commandLine "java", "-classpath", sourceSets.jToolsExamples.runtimeClasspath.getAsPath(), "com.bw.jtools.examples.data.DataDemo" 
}

task runExceptionDialogDemo(type: Exec) {
    dependsOn jToolsExamplesJar
    group = "Examples"
    description = "Runs ExceptionDialogDemo"
    commandLine "java", "-classpath", sourceSets.jToolsExamples.runtimeClasspath.getAsPath(), "com.bw.jtools.examples.exceptiondialog.JExceptionDialogDemo" 
}
task runProfilingDemo(type: Exec) {
    dependsOn jToolsExamplesJar
    group = "Examples"
    description = "Runs Profiling Demo"
    commandLine "java", "-classpath", sourceSets.jToolsExamples.runtimeClasspath.getAsPath(), "com.bw.jtools.examples.profiling.ProfilingDemo", "-file=Test.mm" , "-workLoop=10000", "-repeat=50"
}

task runProfilingWeaverDemo(type: Exec) {
    dependsOn jProfilingAgentJar
    dependsOn jToolsExamplesJar
    group = "Examples"
    description = "Runs Profiling Weaver Demo"
    // Configuration of ProfilingWeaver via system properties:
    //   profiling.weaver.regex    RegExp to match class/method-names.
    //   profiling.weaver.verbose  'true' or 'false' 
    
    commandLine "java", "-classpath", sourceSets.jToolsExamples.runtimeClasspath.getAsPath(),
        "--add-opens", "java.base/java.lang=ALL-UNNAMED", "--illegal-access=deny",
        "-Dprofiling.weaver.regex=.*ProfilingWeaverDemo:.*",
        "-Dprofiling.weaver.verbose=false",
        "-javaagent:${project.buildDir}/libs/jProfilingAgent-${project.version}.jar",
         "com.bw.jtools.examples.profiling.ProfilingWeaverDemo", "-mmFile=build/Test.mm", "-jsonFile=build/Test.json" , "-workLoop=10000", "-repeat=20"
}

task runProfilingWeaverDemoVerbose(type: Exec) {
    dependsOn jProfilingAgentJar
    dependsOn jToolsExamplesJar
    group = "Examples"
    description = "Runs Profiling Weaver Demo in Verbose Mode"
    commandLine "java", "-classpath", "${project.buildDir}/libs/jToolsExamples-${project.version}.jar",
        "--add-opens", "java.base/java.lang=ALL-UNNAMED", "--illegal-access=deny",
        "-javaagent:${project.buildDir}/libs/jProfilingAgent-${project.version}.jar=jar:file:\$CODESOURCEjToolsExamples-${project.version}.jar!/com/bw/jtools/examples/profiling/Weaver.properties",
        "com.bw.jtools.examples.profiling.ProfilingWeaverDemo", 
        "-mmFile=build/Test.mm", "-jsonFile=build/Test.json", "-jsonPrettyFile=build/TestPretty.json",
        "-workLoop=10000", "-repeat=20"
}

task debugProfilingWeaverDemoVerbose(type: JavaExec) {
    dependsOn jProfilingAgentJar
    dependsOn jToolsExamplesJar
    group = "Examples"
    description = "Debug Profiling Weaver Demo in Verbose Mode"
    main = "com.bw.jtools.examples.profiling.ProfilingWeaverDemo"
    classpath = layout.files("${project.buildDir}/libs/jToolsExamples-${project.version}.jar")
    jvmArgs = [ \
        "-Dprofiling.weaver.regex=.*ProfilingWeaverDemo:.*",\
        "-Dprofiling.weaver.verbose=true",\
        "--add-opens", "java.base/java.lang=ALL-UNNAMED", "--illegal-access=deny", \
        "-javaagent:${project.buildDir}/libs/jProfilingAgent-${project.version}.jar" 
        // "-javaagent:${project.buildDir}/libs/jProfilingAgent-${project.version}.jar=jar:file:\$CODESOURCE$jToolsExamples-${project.version}.jar!/com/bw/jtools/examples/profiling/Weaver.properties" 
    ]
    args = [ "-mmFile=build/Test.mm", "-jsonFile=build/Test.json", "-jsonPrettyFile=build/TestPretty.json", "-workLoop=10000", "-repeat=2" ]
    debug = true
}


task runPropTableDemo(type: Exec) {
    dependsOn jToolsExamplesJar
    group = "Examples"
    description = "Runs PropertyTable Demo"
    commandLine "java", "-classpath", sourceSets.jToolsExamples.runtimeClasspath.getAsPath(), "com.bw.jtools.examples.propertytable.PropertyTableDemo"
}

publishing {
    repositories {        
        maven {
            url "https://oss.sonatype.org/service/local/staging/deploy/maven2"
            credentials {
                // Place this two variable with the correct values in your 
                // ~/.gradle/gradle.properties
                username sonatypeUsername
                password sonatypePassword
            }
        }
    }
    publications {
        jtools(MavenPublication) {
            artifactId = 'jtools'
            from components.java
            
            artifacts = [ jToolsJar, jToolsSourcesJar, jToolsJavadocJar ]
            
            pom {
                name = 'jAppTools'
                description = 'A minimalistic set of tools for rapid application development'
                url = 'https://github.com/BWeng20/bwJTools'
                licenses {
                    license {
                        name = 'MIT License'
                    }
                }
                developers {
                    developer {
                        id = 'bweng20'
                        name = 'Bernd Wengenrot'
                        email = 'Bernd.Wengenrot@gmail.com'
                    }
                }
                scm {
                    connection = 'scm:git:git://github.com/BWeng20/bwJTools.git'
                    developerConnection = 'scm:git:ssh://github.com:BWeng20/bwJTools.git'
                    url = 'https://github.com/BWeng20/bwJTools'
                }                
            }
        }
        jProfilingAgent(MavenPublication) {
            artifactId = 'jProfilingAgent'
            from components.java
            
            artifacts = [ jProfilingAgentJar, jProfilingAgentSourcesJar, jProfilingAgentJavadocJar ]
            
            pom {
                name = 'bwJProfiling'
                description = 'A minimalistic long term profiling framework'
                url = 'https://github.com/BWeng20/bwJTools'
                licenses {
                    license {
                        name = 'MIT License'
                    }
                }
                developers {
                    developer {
                        id = 'bweng20'
                        name = 'Bernd Wengenrot'
                        email = 'Bernd.Wengenrot@gmail.com'
                    }
                }
                scm {
                    connection = 'scm:git:git://github.com/BWeng20/bwJTools.git'
                    developerConnection = 'scm:git:ssh://github.com:BWeng20/bwJTools.git'
                    url = 'https://github.com/BWeng20/bwJTools'
                }                
            }
        }        
    }
}
    
signing {
    sign publishing.publications.jtools
    sign publishing.publications.jProfilingAgent
}