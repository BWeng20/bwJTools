plugins {
    id 'maven-publish'
}

tasks.withType(JavaCompile) {
    sourceCompatibility= JavaVersion.VERSION_1_8
    targetCompatibility= JavaVersion.VERSION_1_8
}

apply plugin: 'java'
apply plugin: 'jacoco'
apply plugin: 'maven'

repositories {
    jcenter()
}

project.version="1.0"

task copyToDist(type: Copy) {
    from configurations.compile
    into "${buildDir}/output/dist"
}

task fatJar(type: Jar) {
    description = "Create a jar with all dependencies."
    manifest {
        attributes 'Implementation-Title': 'jtools',  
        	   'Implementation-Version': '1.0',
        	   'Main-Class': 'com.bw.jtools.ui.properties.PropertyTableDemo'
    }
    baseName = project.name + '-all'
    excludes = [ '**/com/bw/jtools/examples/**' ]
    from { configurations.compile.collect { it.isDirectory() ? it : zipTree(it) } }
    with jar
}

jar {
    excludes = [ '**/com/bw/jtools/examples/**' ]
}

task profileJar(type: Jar) {
    description = "Create a jar only with Profiling stuff."
    group = "Build"
    baseName = "bwJProfiling"
    includes = [ '**/com/bw/jtools/Log**', '**/com/bw/jtools/profiling/**', '**/com/bw/jtools/log/**' ]
    includeEmptyDirs = false
    from { configurations.compile }
    with jar
}

javadoc {
        doLast {
            copy {
                from "src/main/java"
                include "**/doc-files/**"
                into "$buildDir/docs/javadoc"
            }
        }
    }

task javadocJar(type: Jar) {
    from javadoc
    group = "Documentation"
    classifier = 'javadoc'
}

configurations {
    runtimeOnly {
        exclude group: 'org.apache.logging.log4j', module: 'log4j-api'
        exclude group: 'org.apache.logging.log4j', module: 'log4j-core'
        /* exclude group: 'org.apache.commons', module: 'commons-csv' --> */
    }
}


dependencies {
    testCompile 'junit:junit:4.12'
    compile group: 'org.netbeans.api', name: 'org-netbeans-swing-outline', version: 'RELEASE110', classifier: 'sources'
    compile group: 'org.netbeans.api', name: 'org-netbeans-swing-outline', version: 'RELEASE110'
    compile group: 'org.apache.logging.log4j', name: 'log4j-api', version: '2.12.0'
    compile group: 'org.apache.logging.log4j', name: 'log4j-core', version: '2.12.0'
    compile group: 'javax.json', name: 'javax.json-api', version: '1.1.4'
    compile group: 'org.apache.commons', name: 'commons-csv', version: '1.1'
    compile group: 'javax.interceptor', name: 'javax.interceptor-api', version: '3.1'
}

task runProfileDemo(type: Exec) {
    dependsOn build
    group = "Examples"
    description = "Run ProfileDemo"
    commandLine "java", "-classpath", sourceSets.main.runtimeClasspath.getAsPath(), "com.bw.jtools.examples.profiling.ProfilingDemo", "-file=Test.mm" , "-workLoop=100000", "-repeat=50"
}
